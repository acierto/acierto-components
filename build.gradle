apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'sonar-runner'
apply from: "./libraries.gradle"

configurations {
    jruby
}

dependencies {
    jruby 'org.jruby:jruby-complete:1.7.3'

    // If using Spock, need to depend on geb-spock
    testCompile "org.gebish:geb-spock:$geb_version"
    testCompile "org.spockframework:spock-core:0.7-groovy-2.0"

    // If using JUnit, need to depend on geb-junit (3 or 4)
    testCompile "org.gebish:geb-junit4:$geb_version"
}

buildscript {

    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        classpath 'com.eriwen:gradle-css-plugin:1.2.1'
        classpath "org.codehaus.sonar:sonar-plugin-api:3.5.1"
    }

    apply from: file('gradle/buildscript.gradle'), to: buildscript
}

allprojects {

    repositories {
        mavenCentral()
        mavenLocal()
    }
}

configure (allprojects) { p ->
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'maven'
}

configure (subprojects) {
    sonarRunner {
        sonarProperties {
            property "sonar.projectName", ':aciertoteam-common'
            property "sonar.sourceEncoding", "UTF-8"
            property "sonar.language", "java"
        }
    }

    apply from: file('../gradle/jacoco.gradle')
}

task watchSass {

    println "CSS Directory: $cssDir"
    println "Sass Directory: $sassDir"
    println "Images Directory: $imagesDir"
    println "Javascript Directory: $javascriptsDir"
    println "Gems Directory: $gemsDir"

    doFirst {

        cssDir.mkdirs()

        Thread.start {
            project.javaexec {
                classpath = configurations.jruby
                main = 'org.jruby.Main'
                args "-X-C -S compass watch --sass-dir $sassDir --css-dir $cssDir --images-dir $imagesDir --javascripts-dir $javascriptsDir --relative-assets".tokenize()
                environment 'GEM_PATH', gemsDir
                environment 'PATH', "$gemsDir/bin"
            }
        }
    }
}

task installGems(type: JavaExec) {
    outputs.dir gemsDir

    classpath = configurations.jruby
    main = "org.jruby.Main"
    args "-S gem install -i $gemsDir --no-rdoc --no-ri compass".tokenize()

    doFirst {
        gemsDir.mkdirs()
    }
}

task compileSass(type: JavaExec) {
    outputs.dir cssDir
    inputs.files installGems
    inputs.dir sassDir
    inputs.dir imagesDir
    inputs.dir javascriptsDir

    classpath = configurations.jruby
    main = "org.jruby.Main"
    args "-S compass compile --sass-dir $sassDir --css-dir $cssDir --images-dir $imagesDir --javascripts-dir $javascriptsDir --relative-assets".tokenize()
    environment 'GEM_PATH', "$gemsDir"
    environment 'PATH', "$gemsDir/bin"

    doFirst {
        cssDir.mkdirs()
    }
}

test {
    testLogging.showStandardStreams = true
}

subprojects {
    group = "com.aciertoteam"
    sourceCompatibility = 1.7
}

project(":aciertoteam-test") {
    sonarRunner {
        skipProject = true
    }
}

sonarRunner {
    sonarProperties {
        property "sonar.projectKey", "Acierto_Components"
        property "sonar.projectName", "Acierto_Components"
        property "sonar.login", "admin"
        property "sonar.password", "FAst%aPpoiNtS0nar"
        property "sonar.host.url", "http://localhost:9000"
        property "sonar.jdbc.url", "jdbc:mysql://localhost:3306/sonar"
        property "sonar.jdbc.driverClassName", "com.mysql.jdbc.Driver"
        property "sonar.jdbc.username", "sonar"
        property "sonar.jdbc.password", "t0ps3cr3t"

        property "sonar.core.codeCoveragePlugin", "jacoco"
        property "sonar.jacoco.reportPath", "tests/jacoco-exec/jacoco.exec"
        property "sonar.dynamicAnalysis", "reuseReports"
        property "sonar.surefire.reportsPath", "tests/test-reports"
    }
}